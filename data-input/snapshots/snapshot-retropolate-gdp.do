//----------------------------------------------------------------------------//
//           Snapshot Retropolate GDP. dta
//----------------------------------------------------------------------------//

* Note: Previously, the script import exchange-rate.do was pulling data from the
* files gdp.data and retropolate-gdp.dta, which were generated later in the 
* execution order of main.do.
* This do-file is intended to create the necessary datasets and ensure that this 
* data is stationary in the input files, so it no longer needs to be generated 
* within main.do. By doing this, it eliminates the need for those files and 
* allows the main script to run without looping dependencies.
* Last Update: 31-10-24

//--------------- Data Import ------------------------------------------------//
use "$work_data/retropolate-gdp.dta", clear

//--------------- Data cleanning  --------------------------------------------//
*keep if  year >= 1970

//--------------- Select data for North Korea --------------------------------//
preserve
	keep if iso == "KP"
	keep if year == $pastyear
	
	tempfile north_korea
	save `north_korea'
restore

//--------------- Select data for Yugoslavia ---------------------------------//
preserve
	keep if inlist(iso, "BA", "HR", "MK", "RS", "YU", "KS", "SI", "ME")
	
	tempfile yugoslavia
	save `yugoslavia'
restore

//--------------- Select data from USRR --------------------------------------//
keep if inlist(iso, "AM", "AZ", "BY", "KG", "KZ", "TJ", "TM") | ///
		inlist(iso, "UZ", "EE", "LT", "LV", "MD", "GE") | ///
		inlist(iso, "RU", "UA", "SU")
		

//--------------- Append Everything ------------------------------------------//
append using `north_korea'
append using `yugoslavia'

//--------------- Check consistency ------------------------------------------//
gduplicates tag iso year, gen(duplicate)
assert duplicate==0
drop duplicate

//--------------- Save the data ----------------------------------------------//
label data "Generated by snapshot-retropolate-gdp.do"
save "$input_data_dir/snapshots/snapshot-SUYU-retropolate-gdp-output.dta", replace
