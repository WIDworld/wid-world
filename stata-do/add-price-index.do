* Call price index
use "$work_data/price-index.dta", clear
rename index value
generate p = "pall"
generate widcode = "inyixx999i"
tempfile priceindex
save "`priceindex'"

* Call Local currency unit change indicator 
* Note: i.e. change in local currency unit: e.g. FF, DM, etc. become EUR in 1999, etc.)
* Import Data
use "$wid_dir/Country-Updates/WBOP_NP2025/NievasPiketty2025WBOP.dta", clear
drop if inlist(substr(iso, 1, 1), "X", "O") | inlist(iso, "QM","WO","QE")
keep if origin =="I1d"
generate p = "pall"

drop origin concept
* Generate Fivelets as defined in the Wid-Dictionary
gen widcode = "intlcu999i"

tempfile indexchange
save "`indexchange'"
	
	
*Call data from WID
use "$work_data/convert-to-real-output.dta", clear
drop if (widcode == "inyixx999i") | (widcode == "icpixx999i")

*Append price indexes
append using "`priceindex'"
append using "`indexchange'"
sort iso widcode p year

*complete currency by country
egen currency2 = mode(currency), by(iso)
drop currency
rename currency2 currency

order iso year widcode p value currency

compress
label data "Generated by add-price-index.do"
save "$work_data/add-price-index-output.dta", replace

