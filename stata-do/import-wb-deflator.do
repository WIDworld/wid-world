import delimited "$wb_data/deflator/API_NY.GDP.DEFL.ZS_DS2_en_csv_v2.csv", ///
	rowrange(3) varnames(4) clear encoding("utf8")
foreach v of varlist v5-v61 {
	local year: var label `v'
	if ("`year'" == "") {
		drop `v'
	}
	else {
		rename `v' year`year'
	}
}

drop indicatorname indicatorcode

reshape long year, i(countryname countrycode) j(j)
rename year value
rename j year
cap drop v62 v63

// Identify countries
countrycode countryname, generate(iso) from("wb")
drop countrycode

// Add currency from the metadata
merge n:1 countryname using "$work_data/wb-metadata.dta", ///
	keep(master match) assert(match) nogenerate
drop countryname
	
// Identify currencies
currencycode currency, generate(currency_iso) iso2c(iso) from("wb")
drop currency
rename currency_iso currency

// Correct fiscal year
egen id = group(iso)
xtset id year

replace fiscalyearend = strtrim(fiscalyearend)

generate newvalue = .

replace newvalue = 0.53*value + (1 - 0.53)*F.value if (fiscalyearend == "July 14")
replace newvalue = 0.51*value + (1 - 0.51)*F.value if (fiscalyearend == "July 7")
replace newvalue = 0.50*value + (1 - 0.50)*F.value if (fiscalyearend == "June 30")
replace newvalue = (1 - 0.22)*value + 0.22*L.value if (fiscalyearend == "March 20")
replace newvalue = 0.75*value + (1 - 0.75)*F.value if (fiscalyearend == "September 30")
replace newvalue = (1 - 0.25)*value + 0.25*L.value if (fiscalyearend == "March 31")


replace value = newvalue if (fiscalyearend != "")
drop fiscalyearend newvalue id
drop if value >= .
xtset, clear

keep if (value < .)
drop if (value == 0)

rename value def_wb

// Correct for Indonesian deflator before 1965 (100 times too high)
replace def_wb=def_wb/100 if year<=1965 & iso=="ID"

label data "Generated by import-wb-deflator.do"
save "$work_data/wb-deflator.dta", replace
