use "$work_data/harmonize-units-ouput.dta", clear

// Remove the real serie when the nominal serie also exists
duplicates tag iso year widcode p, generate(duplicate)
egen count_real = total(real == 1) if duplicate, by(iso year widcode p)
egen count_nomi = total(real == 0) if duplicate, by(iso year widcode p)
// Sanity check
assert count_real == 1 if duplicate
assert count_nomi == 1 if duplicate
drop if (duplicate == 1) & (real == 1)
// There should not be any real macro serie left
assert (real == 0) if (substr(widcode, 1, 1) == "m") & (substr(widcode, 4, 3) != "toq")
drop duplicate count_nomi count_real

// Extract price indices and re-introduce them in the database, but making
// sure they are present for all percentiles
tempfile widdb
save "`widdb'"

keep if widcode == "icpixx999i"
keep iso year value index_refyear
rename value cpi
rename index_refyear cpi_refyear
// Sanity check
assert inrange(cpi, .999, 1.001) if (year == cpi_refyear)
tempfile indice
save "`indice'"

use "`widdb'", clear
merge n:1 iso year using "`indice'", nogenerate

// Choose the relevant price index depending on the variable: use CPI
// in priority for distributed variables, and deflator in priority
// for macro variables.
generate priceindex = cpi if inlist(substr(widcode, 1, 1), "a", "t") ///
	& (real == 1) & (real_refyear == cpi_refyear) & (cpi < .)

replace priceindex = cpi if iso=="AU" & inlist(substr(widcode, 1, 1), "a", "t") ///
	& (real == 1) & (cpi < .)

// Sanity check
assert (priceindex < .) if (real == 1)

// Convert to nominal
replace value = value*priceindex if (real == 1)

// Housekeeping
keep year widcode value p iso currency
order iso p widcode year value
sort iso p widcode year

label data "Generated by convert-to-nominal.do"
save "$work_data/convert-to-nominal-output.dta", replace
