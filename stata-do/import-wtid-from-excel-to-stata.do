// List countries in the WID database
import excel using "$wtid_data/Database.xls", sheet("Sources") firstrow ///
	case(lower) allstring clear
quietly levelsof country, local(countrylist)

// Import data from each country
tempfile tmp
local firstiter 1
foreach country of local countrylist {
	display "--> `country'...", _continue
	quietly {
		import delimited using "$wtid_data/csv/`country'.csv", clear varnames(1) encoding("utf8") stringcols(_all) 

		// Store source
		local source = v2[3]
		
		// Drop unnecessary information
		drop in 2/3
		drop in 4/7
		drop in 6
		
		// Macro to store variable information before reshape
		// (placeholder necessary for Stata not to remove quotes)
		local oldcode     "x"
		local note        "x"
		local unit        "x"
		local category    "x"
		local subcategory "x"
		local variable    "x"
		local subvariable "x"
		
		// List variables
		rename v1 year
		ds year, not
		local columns = r(varlist)
		local k = 1
		foreach col of local columns {
			forvalues i = 1/7 {
				local v`i' = `col'[`i']
			}
		
			local oldcode     `oldcode'     `"`v1'"'
			local note        `note'        `"`v2'"'
			local unit        `unit'        `"`v3'"'
			local category    `category'    `"`v4'"'
			local subcategory `subcategory' `"`v5'"'
			local variable    `variable'    `"`v6'"'
			local subvariable `subvariable' `"`v7'"'
			
			rename `col' value`k'
			local k = `k' + 1
		}
		// Remove the information stored in macros from the dataset
		drop in 1/7
		
		// Reshape
		foreach var of varlist value* {
			replace `var' = subinstr(`var', ",", ".", .) 
			replace `var' = subinstr(`var', " ", "", .) 
		}
		destring *, replace
		*exit 1
		drop if (year >= .) // Drop empty rows
		reshape long value, i(year) j(col)
		
		// Re-assign variable information
		generate source = `"`source'"'
		generate oldcode = ""
		generate note = ""
		generate unit = ""
		generate category = ""
		generate subcategory = ""
		generate variable = ""
		generate subvariable = ""
		quietly levelsof col, local(collist)
		foreach col of local collist {
			// Shift to ignore placeholder
			local colshift = `col' + 1
			
			local v1: word `colshift' of `oldcode'
			local v2: word `colshift' of `note'
			local v3: word `colshift' of `unit'
			local v4: word `colshift' of `category'
			local v5: word `colshift' of `subcategory'
			local v6: word `colshift' of `variable'
			local v7: word `colshift' of `subvariable'
			
			replace oldcode     = `"`v1'"' if (col == `col')
			replace note        = `"`v2'"' if (col == `col')
			replace unit        = `"`v3'"' if (col == `col')
			replace category    = `"`v4'"' if (col == `col')
			replace subcategory = `"`v5'"' if (col == `col')
			replace variable    = `"`v6'"' if (col == `col')
			replace subvariable = `"`v7'"' if (col == `col')
		}
		
		// Housekeeping
		drop col
		drop if value >= .
		drop if oldcode == ""
		
		// Add country and save results
		generate country = "`country'"
		if (`firstiter' == 0) {
			append using "`tmp'"
		}
		save "`tmp'", replace
		local firstiter 0
	}
	display "done"
}

// Save memory
compress

// Correct an unnecessary difference between the notes in the Netherlands
// regarding income composition variables
replace note = "Series expressed as percentage of total income excluding " ///
	+ "capital gains. The sum of all sources add up to 100%. Between 1950 " ///
	+ "and 1975, estimates based on tabulated data produced by the Central " ///
	+ "Bureau of Statistics; from 1977, estimated based on micro-data Income " ///
	+ "Panel Survey (IPO) using tax and other administrative data." ///
	if (country == "Netherlands") & substr(oldcode, 1, 3) == "102"

// Stata won't import the source for Greece
replace source = "Wealth: Nikolaos Charalampidis, “The National Wealth-Income " + ///
	"Ratio in Greece, 1974-2013”, forthcoming, The Review of Income and Wealth." if (country == "Greece")
	
//	Correct Indian source
replace source="Income: Banerjee, Abhijit and Piketty, Thomas (2005). " ///
+ "Top Indian Incomes 1922-2000; in Atkinson, A. B. and Piketty, T. (editors) Top Incomes: " ///
+ "A Global Perspective, Oxford University Press, chapter 1." if (country == "India")

label data "Generated by import-wtid-from-excel-to-stata.do"
save "$work_data/original-wtid-db.dta", replace
