import delimited "$wb_data/deflator/API_NY.GDP.DEFL.ZS_DS2_en_csv_v2_$pastyear.csv", ///
	rowrange(3) varnames(4) clear encoding("utf8")

foreach v of varlist v*{
	local year: var label `v'
	if ("`year'" == "") {
		drop `v'
	}
	else {
		rename `v' year`year'
	}
}

dropmiss
drop indicatorname indicatorcode

reshape long year, i(countryname countrycode) j(j)
rename year value
rename j year

// Identify countries
replace countryname = "Macedonia, FYR" if countryname == "North Macedonia"
replace countryname = "Swaziland"      if countryname == "Eswatini"
replace countryname = "Korea, Dem. People's Rep." if countryname == "Korea, Dem. Peopleâ€™s Rep."

countrycode countryname, generate(iso) from("wb")
drop countrycode

// Add currency from the metadata
merge n:1 countryname using "$work_data/wb-metadata.dta", ///
	keep(master match) nogenerate  // Regions are dropped
drop countryname
	
// Identify currencies
replace currency = "turkmenistan manat" if currency == "New Turkmen manat"
currencycode currency, generate(currency_iso) iso2c(iso) from("wb")
drop currency
rename currency_iso currency

// Correct fiscal year
egen id = group(iso)
xtset id year

replace fiscalyearend = strtrim(fiscalyearend)

generate newvalue = .

replace newvalue = 0.53*value + (1 - 0.53)*F.value if (fiscalyearend == "July 14")
replace newvalue = 0.51*value + (1 - 0.51)*F.value if (fiscalyearend == "July 7")
replace newvalue = 0.50*value + (1 - 0.50)*F.value if (fiscalyearend == "June 30")
replace newvalue = (1 - 0.22)*value + 0.22*L.value if (fiscalyearend == "March 20")
replace newvalue = 0.75*value + (1 - 0.75)*F.value if (fiscalyearend == "September 30")
replace newvalue = (1 - 0.25)*value + 0.25*L.value if (fiscalyearend == "March 31")


replace value = newvalue if (fiscalyearend != "")
drop fiscalyearend newvalue id
drop if value >= .
xtset, clear

keep if (value < .)
drop if (value == 0)

rename value def_wb

// Correct for Indonesian deflator before 1965 (100 times too high)
replace def_wb=def_wb/100 if year<=1965 & iso=="ID"

// Bad data for KY
drop if iso == "KY"

label data "Generated by import-wb-deflator.do"
save "$work_data/wb-deflator.dta", replace
