// use "$work_data/add-researchers-data-real-output.dta", clear
// use "$work_data/add-wealth-distribution-output.dta", clear
use "$work_data/aggregate-regions-output.dta", clear

keep if (substr(widcode, 1, 3) == "mnw")

reshape wide value, i(iso year p currency) j(widcode) string

replace valuemnwhou = valuemnwdwe + valuemnwlan if missing(valuemnwhou)
replace valuemnwbus = valuemnwagr + valuemnwodk + cond(missing(valuemnwnat), 0, valuemnwnat) if missing(valuemnwbus)

replace valuemnwnfa = valuemnwhou + valuemnwbus if missing(valuemnwnfa)

reshape long value, i(iso year p currency) j(widcode) string
drop if missing(value)

tempfile completed
save "`completed'"

keep iso widcode
generate sixlet = substr(widcode, 1, 6)
keep if inlist(sixlet, "mnwhou", "mnwbus", "mnwnfa")

// Associate codes of parent variables
replace sixlet = "mnwdwe" if sixlet == "mnwhou"
replace sixlet = "mnwodk" if sixlet == "mnwbus"
replace sixlet = "mnwdwe" if sixlet == "mnwnfa"
duplicates drop
merge n:1 iso sixlet using "$work_data/aggregate-regions-metadata-output.dta", nogenerate keep(master match)
replace sixlet = substr(widcode, 1, 6)
drop widcode
generate new = 1
// append using "$work_data/add-researchers-data-real-metadata.dta"
// append using "$work_data/add-wealth-distribution-metadata.dta"
append using "$work_data/aggregate-regions-metadata-output.dta"
replace new = 0 if missing(new)
duplicates tag iso sixlet, gen(dup)
drop if new & dup
drop new dup
label data "Generated by complete-variables.do"
save "$work_data/complete-variables-metadata.dta", replace

// use "$work_data/add-researchers-data-real-output.dta", clear
// use "$work_data/add-wealth-distribution-output.dta", clear
use "$work_data/aggregate-regions-output.dta", clear
drop if (substr(widcode, 1, 3) == "mnw")
append using "`completed'"

compress
label data "Generated by complete-variables.do"
save "$work_data/complete-variables-output.dta", replace
