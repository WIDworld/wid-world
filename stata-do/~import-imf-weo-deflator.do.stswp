import delimited "$imf_data/world-economic-outlook/WEO-$pastyear.csv", ///
	clear delimiter(";") varnames(1) encoding("utf8")

dropmiss, obs

foreach v of varlist v* {
	local year: var label `v'
	if ("`year'" == "") {
		drop `v'
	}
	else {
		destring `v', replace force ignore(",")
		rename `v' value`year'
	}
}

keep if weosubjectcode == "NGDP_D" | weosubjectcode == "PPPEX"
drop iso weocountrycode subjectdescriptor units ///
	scale countryseriesspecificnotes


replace country = "Côte d'Ivoire" if country == "C�te d'Ivoire"
replace country = "Côte d'Ivoire" if country == "CÙte d'Ivoire"


replace country = "São Tomé and Príncipe" if country == "S�o Tom� and Pr�ncipe"
replace country = "Côte d'Ivoire"         if country == "Cte d'Ivoire"
replace country = "São Tomé and Príncipe" if country == "S„o TomÈ and PrÌncipe"
replace country = "São Tomé and Príncipe" if country == "So Tom and Prncipe"
replace country = "Côte d'Ivoire"         if country == "C�te d'Ivoire"
replace country = "São Tomé and Príncipe" if country == "S�o Tom� and Pr�ncipe"
replace country = "Swaziland"             if country == "Eswatini"
replace country = "Turkey"             	  if country == "T¸rkiye"

countrycode country, generate(iso) from("imf weo")
drop country

drop subjectnotes
reshape long value, i(iso weosubjectcode) j(year)
reshape wide value, i(iso year) j(weosubjectcode) string

// Zimbabwe: IMF moved to RTGS dollars unlike other databases: convert back to USD
replace valueNGDP_D = valueNGDP_D/valuePPPEX if iso == "ZW"
drop valuePPPEX


rename valueNGDP_D def_imf

drop if def_imf >= .

drop if year >=$year  // we are taking 2021 estimates

rename def_imf def_weo

label data "Generated by import-imf-weo-deflator.do"
save "$work_data/imf-deflator-weo.dta", replace
