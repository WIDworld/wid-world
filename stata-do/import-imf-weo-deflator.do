import delimited "$imf_data/world-economic-outlook/WEO-$pastyear.csv", ///
	clear delimiter(";") varnames(1) encoding("utf8")

dropmiss, obs force

foreach v of varlist v* {
	local year: var label `v'
	if ("`year'" == "") {
		drop `v'
	}
	else {
		destring `v', replace force ignore(",")
		rename `v' value`year'
	}
}

keep if weosubjectcode == "NGDP_D" | weosubjectcode == "PPPEX"
drop iso weocountrycode subjectdescriptor units ///
	scale countryseriesspecificnotes

/* 
replace country="Côte d'Ivoire" if country=="C�te d'Ivoire"
replace country="São Tomé and Príncipe" if country=="S�o Tom� and Pr�ncipe"
*/

replace country = "Côte d'Ivoire"         if country=="Cte d'Ivoire"
replace country = "São Tomé and Príncipe" if country=="So Tom and Prncipe"
replace country = "Swaziland"             if country == "Eswatini"


countrycode country, generate(iso) from("imf weo")
drop country


reshape long value, i(iso weosubjectcode) j(year)
preserve 
	keep iso year weosubjectcode subjectnotes 
	drop if weosubjectcode == "PPPEX"
	drop weosubjectcode
	duplicates drop iso year subjectnotes, force
	tempfile note
	save `note'
restore 
reshape wide value, i(iso year) j(weosubjectcode) string

// Zimbabwe: IMF moved to RTGS dollars unlike other databases: convert back to USD
replace valueNGDP_D = valueNGDP_D/valuePPPEX if iso == "ZW"
drop valuePPPEX

merge m:1 iso year using `note', nogen

rename valueNGDP_D def_imf

drop if def_imf >= .

drop if year>$pastyear

rename def_imf def_weo

label data "Generated by import-imf-weo-deflator.do"
save "$work_data/imf-deflator-weo.dta", replace
