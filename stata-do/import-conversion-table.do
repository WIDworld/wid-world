//------------------------------------------------------------------------------
//                 Import Conversion Table.do
//------------------------------------------------------------------------------
// Objective: Generate a link table between the oldcodes of the WTID dataset and 
//            the current widcodes.

import excel using "$codes_dictionary", ///
	sheet("Wealth_Macro_Variables") cellrange(D4:F140) clear allstring
keep D F
rename D widcode
rename F oldcode

replace widcode = usubinstr(widcode, "*", "m", 1)

tempfile codes
save "`codes'"

// Wealth: distributed variables
import excel using "$codes_dictionary", ///
	sheet("Wealth_Distributed_Variables") cellrange(D4:F22) clear allstring
keep D F
rename D widcode
rename F oldcode

replace widcode = usubinstr(widcode, "*", "a", 1)

append using "`codes'"
save "`codes'", replace

// Income: distributed variables
import excel using "$codes_dictionary", ///
	sheet("Income_Distributed_Variables") cellrange(D4:F123) clear allstring
keep if strpos(D,"*")
keep D F
rename D widcode
rename F oldcode

replace widcode = usubinstr(widcode, "*", "a", 1)

append using "`codes'"
save "`codes'", replace

// Income: macroeconomic variables
import excel using "$codes_dictionary", ///
	sheet("Income_Macro_Variables") cellrange(D4:F331) clear allstring
keep D F
rename D widcode
rename F oldcode

replace widcode = usubinstr(widcode, "*", "m", 1)

append using "`codes'"
save "`codes'", replace

// Macro variables: other
import excel using "$codes_dictionary", ///
	sheet("Other_Macro_Variables") cellrange(D4:F18) clear allstring
keep D F
rename D widcode
rename F oldcode

replace widcode = usubinstr(widcode, "*", "m", 1)

append using "`codes'"
save "`codes'", replace

// Other WTID variables
import excel using "$codes_dictionary", ///
	sheet("Other_WTID") cellrange(A4:B123) clear allstring
rename A oldcode
rename B widcode

append using "`codes'"
save "`codes'", replace

// Missing WTID codes of National Accounts
import excel using "$codes_dictionary", ///
	sheet("Old_CodesWTID") cellrange(A4:K126) clear allstring
keep A K
drop if missing(A) | missing(K)
rename A oldcode
rename K widcode
keep  if substr(oldcode, 1, 1) == "3"
gen miss=1

replace widcode ="mdsavi" if widcode=="dsavi"
replace widcode ="mfsavi" if widcode=="fsavi"

append using "`codes'"
save "`codes'", replace

//Cleaning
drop if missing(oldcode) | missing(widcode)
duplicates drop oldcode widcode, force

duplicates tag oldcode, generate(dup_lost)
drop if miss==1 & dup_lost!=0 

drop miss dup_lost

// Drop excluded variables
drop if inlist(widcode,"mssdep","mssavi","mssgro")
*duplicates drop

//Saving
label data "Generated by import-conversion-table.do"
save "$work_data/correspondance-table.dta", replace
