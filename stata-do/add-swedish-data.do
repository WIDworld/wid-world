
import excel "$wid_dir/Country-Updates/Sweden/2017/Sweden_WID.world_2.xlsx", clear

// Clean
renvars, map(strtoname(@[3]))
drop if _n<4
destring _all, replace
rename WID_code year
dropmiss, force

// Change units
foreach var of varlist m*{
replace `var'=`var'*1000000000
}
foreach var of varlist n*{
replace `var'=`var'*1000
}
replace mcwtoq999i=mcwtoq999i/1000000000 // Tobin Q not to be multiplied

// Reshape
ds year, not
renvars `r(varlist)', pref(value)
reshape long value, i(year) j(widcode) string
drop if mi(value)
gen iso="SE"
gen currency = "SEK" if inlist(substr(widcode, 1, 1), "a", "t", "m", "i")
gen p="pall"

drop if widcode=="inyixx999i"

levelsof widcode, local(levels)

tempfile sweden
save "`sweden'"

// Create metadata
generate sixlet = substr(widcode, 1, 6)
keep iso sixlet
duplicates drop
generate source = `"[URL][URL_LINK][/URL_LINK]"' ///
	+ `"[URL_TEXT]Waldenström, D. (2017). "' ///
	+ `"Wealth-income ratios in a small, developing economy: Sweden, 1810–2014[/URL_TEXT][/URL]; "'
generate method = ""
tempfile meta
save "`meta'"

// Add data to WID
use "$work_data/add-spanish-data-output.dta", clear
gen oldobs=1
append using "`sweden'"
duplicates tag iso year p widcode, gen(dup)
qui count if dup==1 & iso!="SE"
assert r(N)==0
drop if oldobs==1 & dup==1
drop oldobs dup

label data "Generated by add-swedish-data.do"
save "$work_data/add-swedish-data-output.dta", replace

// Add metadata
use "$work_data/add-spanish-data-metadata.dta", clear
merge 1:1 iso sixlet using "`meta'", nogenerate update replace

label data "Generated by add-swedish-data.do"
save "$work_data/add-swedish-data-metadata.dta", replace



