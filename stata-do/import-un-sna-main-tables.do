// GDP
import delimited "$un_data/sna-main/gdp/gdp-current-$pastyear.csv", ///
	clear delimiter(",") encoding("utf8")
cap rename countryarea countryorarea
rename grossdomesticproduct gdp
cap ren unit currency

tempfile gdp
save "`gdp'"

// GNI
import delimited "$un_data/sna-main/gni/gni-current-$pastyear.csv", ///
	clear delimiter(",") encoding("utf8")

cap rename countryarea countryorarea
cap ren unit currency

// Dropping Ethiopian GNI 1987-1989 (no GDP data)
*drop if countryorarea=="Ethiopia" & inlist(year,1987,1988,1989)

rename grossnationalincome gni
merge 1:1 countryorarea year using "`gdp'", assert(match) nogenerate

save "`gdp'", replace

// GDP Current USD
import delimited "$un_data/sna-main/gdp/gdp-usd-current-$pastyear.csv", ///
	clear delimiter(",") encoding("utf8")
cap rename countryarea countryorarea
drop if unit == "..."
cap drop unit
rename grossdomesticproduct gdp_usd
replace gdp_usd = subinstr(gdp_usd, ",", ".", 1)
destring gdp_usd, replace
merge 1:1 countryorarea year using "`gdp'", assert(match master) keep(match) nogenerate
merge 1:1 countryorarea year using "`gdp'", keep(match) nogenerate //Regions are dropped 

drop if currency == "..."
replace gni = subinstr(gni, ",", ".", 1)
replace gdp = subinstr(gdp, ",", ".", 1)
destring gni gdp, replace
*confirm numeric variable gni gdp

replace countryorarea = "Côte d'Ivoire" if (countryorarea == "C�te d'Ivoire")
replace countryorarea = "Curaçao" if (countryorarea == "Cura�ao")
replace countryorarea = "Swaziland" if (countryorarea == "Kingdom of Eswatini")
replace countryorarea = "Czech Republic" if (countryorarea == "Czechia")


// Identify countries ------------------------------------------------------- //
countrycode countryorarea, generate(iso) from("un sna main")
drop countryorarea

// Deal with former economies
drop if (iso == "ET") & (year < 1993)
drop if (iso == "ER") & (year < 1993)
drop if (iso == "ET-FORMER") & (year >= 1993)
replace iso = "ET" if (iso == "ET-FORMER")
replace currency = "ethiopian birr" if (iso == "ET")
replace iso = "SD" if (iso == "SD-FORMER")

// Sanity check: only one currency by country
replace currency = strtrim(stritrim(strlower(currency)))
egen ncu = nvals(currency), by(iso)
assert ncu == 1
drop ncu

// Convert to Israeli New Shekel for the State of Palestine
tempfile unsna
save "`unsna'"

import delimited "$oecd_data/exchange-rates/ils-usd-$pastyear.csv", clear
generate iso = "PS"
rename time year
rename value exch
keep iso year exch
tempfile exch
save "`exch'"

use "`unsna'", clear
merge n:1 iso year using "`exch'", nogenerate keep(master match)
replace gni = gni*exch if (iso == "PS")
replace gdp = gdp*exch if (iso == "PS")
replace currency = "new israeli sheqel" if (iso == "PS")
drop exch

// Identify currencies ------------------------------------------------------ //
replace currency = "us dollar" if currency == "us$"

currencycode currency, generate(currency_iso) iso2c(iso) from("un sna main")
drop currency
rename currency_iso currency

// Convert from Czechoslovak koruna to Czech koruna (at par)
replace currency = "CZK" if (currency == "CSK")
// Convert from Soviet Ruble to new Russian Ruble (1:1000)
replace gni = gni/1000 if (currency == "SUR")
replace gdp = gdp/1000 if (currency == "SUR")
replace currency = "RUB" if (currency == "SUR")
// Correct some error in North Korea
replace gni = gni*100 if (iso == "KP") & (year <= 2001)
replace gdp = gdp*100 if (iso == "KP") & (year <= 2001)

// Correct fiscal year ------------------------------------------------------ //
reshape long g, i(iso year) j(indicator) string

egen id = group(iso indicator)
xtset id year

generate newvalue = .

// See: http://unstats.un.org/unsd/snaama/notes.asp
replace newvalue = (1 - 0.75)*L.g + 0.75*g ///
	if inlist(iso, "IN", "MM", "NZ")
replace newvalue = (1 - 0.50)*L.g + 0.50*g ///
	if inlist(iso, "AU", "SD", "YA")
replace newvalue = (1 - 0.78)*L.g + 0.78*g ///
	if inlist(iso, "AF", "IR")
replace newvalue = (1 - 0.50)*g + 0.50*F.g ///
	if inlist(iso, "BD", "EG", "NR", "PK", "PR", "TO")
replace newvalue = (1 - 0.25)*g + 0.25*F.g ///
	if inlist(iso, "HT", "MH", "FM", "PW")
replace newvalue = 0.53*g + (1 - 0.53)*F.g ///
	if inlist(iso, "NP")
replace newvalue = 0.51*g + (1 - 0.51)*F.g ///
	if inlist(iso, "ET", "ET-FORMER")

egen hasnew = total(newvalue < .), by(id)
replace g = newvalue if (hasnew)
xtset, clear
drop id newvalue hasnew
keep if (g < .)

reshape wide g, i(iso year) j(indicator) string

// Calculate NFI ------------------------------------------------------------ //
generate nfi_pct_un2 = (gni - gdp)/gdp
rename gdp gdp_lcu_un2
rename gdp_usd gdp_usd_un2

// Drop NFI in Timor-Leste
replace nfi_pct_un2 = . if (iso == "TL")

keep iso currency year gdp* nfi
order iso currency year gdp* nfi

label data "Generated by import-un-sna-main-tables.do"
save "$work_data/un-sna-summary-tables.dta", replace
