// Import all files
import delimited "$wb_data/gdp-current-lcu/API_NY.GDP.MKTP.CN_DS2_en_csv_v2-$pastyear.csv", ///
	clear encoding("utf8") rowrange(3) varnames(4)

tempfile wb_macro_data
save "`wb_macro_data'"

import delimited "$wb_data/gdp-current-usd/API_NY.GDP.MKTP.CD_DS2_en_csv_v2-$pastyear.csv", ///
	clear encoding("utf8") rowrange(3) varnames(4)

append using "`wb_macro_data'"
save "`wb_macro_data'", replace

import delimited "$wb_data/nfi/API_NY.GSR.NFCY.CD_DS2_en_csv_v2-$pastyear.csv", ///
	clear encoding("utf8") rowrange(3) varnames(4)

append using "`wb_macro_data'"

// Rename year variables
dropmiss, force
foreach v of varlist v* {
	local year: variable label `v'
	rename `v' value`year'
}

replace countryname = "Macedonia, FYR" if countryname == "North Macedonia"
replace countryname = "Swaziland" if countryname == "Eswatini"

// Identify countries
countrycode countryname, generate(iso) from("wb")

// Add currency from the metadata
merge n:1 countryname using "$work_data/wb-metadata.dta", ///
	keep(master match) assert(match using) nogenerate

// Identify currencies
currencycode currency, generate(currency_iso) iso2c(iso) from("wb")
drop currency
rename currency_iso currency

drop countryname countrycode

// Change format and reshape
replace indicatorcode = "gdp_lcu_wb" if (indicatorcode == "NY.GDP.MKTP.CN")
replace indicatorcode = "gdp_usd_wb" if (indicatorcode == "NY.GDP.MKTP.CD")
replace indicatorcode = "nfi_usd_wb" if (indicatorcode == "NY.GSR.NFCY.CD")
drop indicatorname

reshape long value, i(iso indicatorcode) j(year)

// Correct fiscal year
egen id = group(iso indicatorcode)
xtset id year

replace fiscalyearend = strtrim(fiscalyearend)

generate newvalue = .

replace newvalue = 0.53*value + (1 - 0.53)*F.value if (fiscalyearend == "July 14")
replace newvalue = 0.51*value + (1 - 0.51)*F.value if (fiscalyearend == "July 7")
replace newvalue = 0.50*value + (1 - 0.50)*F.value if (fiscalyearend == "June 30")
replace newvalue = (1 - 0.22)*value + 0.22*L.value if (fiscalyearend == "March 20")
replace newvalue = 0.75*value + (1 - 0.75)*F.value if (fiscalyearend == "September 30")
replace newvalue = (1 - 0.25)*value + 0.25*L.value if (fiscalyearend == "March 31")

replace value = newvalue if (fiscalyearend != "")
drop fiscalyearend newvalue id
drop if value >= .
xtset, clear

reshape wide value, i(iso year) j(indicatorcode) string
rename valuegdp_lcu_wb gdp_lcu_wb
rename valuegdp_usd_wb gdp_usd_wb
rename valuenfi_usd_wb nfi_usd_wb

// Correct for Indonesia GDP
replace gdp_lcu_wb=gdp_lcu_wb/100 if year<=1965 & iso=="ID"

label data "Generated by import-wb-macro-data.do"
save "$work_data/wb-macro-data.dta", replace
