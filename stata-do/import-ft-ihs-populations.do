*-------------------------------------------------------------------------------
*-------------------------------------------------------------------------------

*---- 1. Import country-codes --------------------------------------------------
/*
*Old source
import excel "$country_codes/country-codes-new.xlsx", clear firstrow
ren shortname country
keep if country!=""
drop if inlist(code, "US-GA","QT")
replace country = subinstr(country, " ", "",.) 
*/


*New Source
use "$work_data/import-country-codes-output.dta", clear
rename (iso shortname) (code country)

drop TH corecountry
drop if strpos(code,"-")
replace country = subinstr(country, " ", "",.) 
order code titlename country region1 region2 region3 region4 region5

*Change name to make it consistent with FT
replace country="Coted'Ivoire" 					if code=="CI"
replace country="BoliviaPlurinationalStateof" 	if code=="BO"
replace country="BritishVirginIslands" 		    if code=="VG"
replace country="CapeVerde" 					if code=="CV"
replace country="ChinaHongKongSAR" 			    if code=="HK"
replace country="ChinaMacaoSAR" 				if code=="MO"
replace country="DemPeoplesRepublicofKorea" 	if code=="KP"
replace country="DemocraticRepublicoftheCongo"  if code=="CD"
replace country="FaeroeIslands" 				if code=="FO"
replace country="FalklandIslandsMalvinas" 		if code=="FK"
replace country="FrenchGuiana" 				    if code=="GF"
replace country="GuineaBissau" 				    if code=="GW"
replace country="IranIslamicRepublicof" 		if code=="IR"
replace country="LaoPeoplesDemocraticRepublic"  if code=="LA"
replace country="LibyanArabJamahiriya" 		    if code=="LY"
replace country="MicronesiaFedStatesof" 		if code=="FM"
replace country="OccupiedPalestinianTerritory"   if code=="PS"
replace country="RepublicofKorea" 				if code=="KR"
replace country="RepublicofMoldova" 			if code=="MD"
replace country="TFYRMacedonia" 				if code=="MK"
replace country="TimorLeste" 					if code=="TL"
replace country="UnitedRepublicofTanzania"   	if code=="TZ"
replace country="UnitedStatesVirginIslands" 	if code=="VI"
replace country="UnitedStatesofAmerica" 		if code=="US"
replace country="VenezuelaBolivarianRepublico"  if code=="VE"
replace country="WallisandFutunaIslands" 		if code=="WF"
replace country="Réunion" 						if code=="RE"

replace country="RepublicofKorea" 				if code=="KR"
replace country="CzechRepublic" 				if code=="CZ"
replace country="Swaziland" 					if code=="SZ"
replace country="SyrianArabRepublic" 			if code=="SY"
replace country="Turkey" 						if code=="TR"
replace country="RussianFederation" 			if code=="RU"


tempfile countrycodes
save "`countrycodes'",replace


*---- 2. Import FT -------------------------------------------------------------
import excel "$federico_tena_data/world-1991-borders_1800-1938_FTWPHD_2025_v01_.xlsx", sheet("COUNTRIES BY CONTINENT") cellrange(A2:IA146) firstrow clear
ren Borders1991 year
drop if year=="" | year=="WORD COUNTRY Nº" 
destring year, replace
drop BG DG FG HD
// keep if year==1938
ren * pop*
ren popyear year

*---- 3. Correct country names  ------------------------------------------------
reshape long pop, i(year) j(country) string

*A little adjustment
replace country="Coted'Ivoire"          if country=="CôtedIvoire"

*----- 4. Merge with WID country names -----------------------------------------
merge m:1 country using "`countrycodes'"

* Compleating the territories
replace code="GF" if country=="FrenchGuiana"

*---- 5. Cleanning and formatting ----------------------------------------------
drop country
rename code iso

*These are the countries for which FT have no data
/*
countrname
Dont have data
BQ Bonaire,SintEustatiusandSaba   - assumme it grows like Aruba (AW)
CW Curacao							- assumme it grows like Aruba (AW)
GG Guernsey							- assumme it grows like UK (GB)
JE Jersey							- assumme it grows like UK (GB)
KS Kosovo							- asumme it grows like Serbia (RS)
SX SintMaarten(Dutchpart)			- assumme it grows like Anguilla (AI)
SS SouthSudan						- assumme it grows like Sudan (SD)

*/
rename pop value
gen widcode="npopul999i"

keep iso year widcode value 
order iso year widcode value 
sort  iso year
duplicates report
tab year
drop if year==.

replace value=value*1000

*---- 6. Export ----------------------------------------------------------------
label data "Generated by import-ft-ihs-populations.do"
save "$work_data/ft-borders1991-population.dta",replace



*-------------------------------------------------------------------------------
**# Import IHS age/gender breakdwons data available from 1800 to 1950        #**
*-------------------------------------------------------------------------------

*---- 2. Import IHS data  ------------------------------------------------------
use "$ihs_data/International_Historical_Statistics_pop.dta", clear

*---- 3. Generate total gender breakdowns --------------------------------------
foreach sex in M F {
	gen  a999`sex'=a04`sex' +a59`sex' +a1014`sex' +a1519`sex' +a2024`sex' +a2529`sex' +a3034`sex' +a3539`sex' +a4044`sex' +a4549`sex' +a5054`sex' +a5559`sex' +a6064`sex' +a6569`sex' +a7074`sex' +a7579`sex' +a80over`sex' +unknown`sex'
}
gen  a999i=a999F+a999M

*---- 4. Formatting ------------------------------------------------------------
//Dropping unkowns because don't know in which breakdwown they fit
drop unknownF unknownM
reshape long a, i(year country) j(value) string
rename country iso
rename value age
rename a value
replace value=value*1000

// Formatting age and sex
tab age
rename age age2

gen sex =substr(age2,5,1)
gen age=substr(age,1,4) if sex!=""
replace age=substr(age2,1,2) if sex==""
replace sex=substr(age2,3,1) if sex==""

replace age=substr(age2,1,2) if sex=="e"
replace sex=substr(age2,7,1) if sex=="e"

replace age=substr(age2,1,3) if sex=="9"
replace sex=substr(age2,4,1) if sex=="9"
replace sex="f" if sex=="F"
replace sex="m" if sex=="M"
tab age sex
drop age2

// Generate proper widcodes
generate widcode = "npopul"

replace widcode = widcode + "999" if (age == "999")

replace widcode = widcode + "991" if (age == "children") //no changes
replace widcode = widcode + "992" if (age == "adults") //no changes

replace widcode = widcode + "993" if (age == "2039")  //no changes
replace widcode = widcode + "994" if (age == "4059")  //no changes
replace widcode = widcode + "995" if (age == "60")  //no changes
replace widcode = widcode + "996" if (age == "2064")  //no changes
replace widcode = widcode + "997" if (age == "65") //no changes
replace widcode = widcode + "998" if (age == "80")

replace widcode = widcode + "001" if (age == "04")
replace widcode = widcode + "051" if (age == "59")
replace widcode = widcode + "101" if (age == "1014")
replace widcode = widcode + "151" if (age == "1519")
replace widcode = widcode + "201" if (age == "2024")
replace widcode = widcode + "251" if (age == "2529")
replace widcode = widcode + "301" if (age == "3034")
replace widcode = widcode + "351" if (age == "3539")
replace widcode = widcode + "401" if (age == "4044")
replace widcode = widcode + "451" if (age == "4549")
replace widcode = widcode + "501" if (age == "5054")
replace widcode = widcode + "551" if (age == "5559")
replace widcode = widcode + "601" if (age == "6064")
replace widcode = widcode + "651" if (age == "6569")
replace widcode = widcode + "701" if (age == "7074")
replace widcode = widcode + "751" if (age == "7579")
replace widcode = widcode + "801" if (age == "8084") //no changes
replace widcode = widcode + "851" if (age == "8589") //no changes
replace widcode = widcode + "901" if (age == "9094") //no changes
replace widcode = widcode + "951" if (age == "9599") //no changes
replace widcode = widcode + "111" if (age == "100") //no changes

replace widcode = widcode + "202" if (age == "2029") //no changes
replace widcode = widcode + "302" if (age == "3039") //no changes
replace widcode = widcode + "402" if (age == "4049") //no changes
replace widcode = widcode + "502" if (age == "5059") //no changes
replace widcode = widcode + "602" if (age == "6069") //no changes
replace widcode = widcode + "702" if (age == "7079") //no changes
replace widcode = widcode + "802" if (age == "8089") //no changes
replace widcode = widcode + "902" if (age == "9099") //no changes

replace widcode = widcode + "i" if (sex == "i")
replace widcode = widcode + "m" if (sex == "m")
replace widcode = widcode + "f" if (sex == "f")

assert strlen(widcode) == 10
drop age sex 
format value %12.0f

*---- 5. Generate new age gender breakdowns (0-14,15-64.65+,0-19,20+)
reshape wide value, i(iso year) j(widcode) string

foreach gender in 001 051 101 151 201 251 301 351 401 451 501 551 601 651 701 751 998  {
	gen valuenpopul`gender'i=valuenpopul`gender'f + valuenpopul`gender'm
}

foreach gender in i f m{
	gen valuenpopul014`gender'=valuenpopul001`gender'+valuenpopul051`gender'+valuenpopul101`gender' 

	gen valuenpopul156`gender'=valuenpopul151`gender' +valuenpopul201`gender' +valuenpopul251`gender' +valuenpopul301`gender' +valuenpopul351`gender' +valuenpopul401`gender' +valuenpopul451`gender' +valuenpopul501`gender' +valuenpopul551`gender' +valuenpopul601`gender' 
	
	gen valuenpopul997`gender'=valuenpopul651`gender' +valuenpopul701`gender'+valuenpopul751`gender' +valuenpopul998`gender' 
	
	
	gen valuenpopul991`gender'=valuenpopul001`gender'+valuenpopul051`gender'+valuenpopul101`gender'+valuenpopul151`gender'
	
	gen valuenpopul992`gender'=(valuenpopul014`gender'+valuenpopul156`gender'+valuenpopul997`gender')-valuenpopul991`gender'
}

keep year iso valuenpopul014* valuenpopul156* valuenpopul997* valuenpopul991* valuenpopul992*
foreach sex in i f m {
	gen valuenpopul999`sex'=valuenpopul014`sex' + valuenpopul156`sex' + valuenpopul997`sex'
}

*Correct mistakes
sum _all
drop if valuenpopul999f==0  // imposible that there were no women, eliminate observation for that country/year
drop if valuenpopul992m==0 // imposible ... no young male men, eliminate observation ...
drop if valuenpopul997i==0 // imposible ... no old age people, eliminate observation ...
drop if valuenpopul997f==0 //// imposible ... no old age women, eliminate observation ...

sum _all


*keep only shares from total population
foreach var in valuenpopul014i valuenpopul156i valuenpopul997i valuenpopul991i valuenpopul992i valuenpopul014f valuenpopul156f valuenpopul997f valuenpopul991f valuenpopul992f valuenpopul014m valuenpopul156m valuenpopul997m valuenpopul991m valuenpopul992m  valuenpopul999f valuenpopul999m{
	replace `var'=round(`var'/valuenpopul999i,.0001)
}
drop valuenpopul999i
reshape long value, i(iso year) j(widcode) string

*---- 6. Cleanning -------------------------------------------------------------
duplicates report iso widcode year

keep if year<1950

*---- 7. Export ----------------------------------------------------------------
label data "Generated by import-ft-ihs-populations.do"
save "$work_data/ihs-breakdowns-population.dta",replace
