use "$work_data/add-exchange-rates-output.dta", clear

merge n:1 iso year using "$work_data/price-index.dta", ///
	nogenerate keepusing(index) keep(master match)

// Attribute US deflator to US States
preserve
	keep if iso=="US"
	keep year index
	duplicates drop
	ren index us_index
	tempfile temp
	save `temp'
restore
merge m:1 year using `temp', nogen
replace index=us_index if substr(iso,1,3)=="US-"
drop us_index

// Check that there is always a price index for the nominal data
assert !missing(index) if (iso != "CZ") ///
	& inlist(substr(widcode, 1, 1), "a", "m", "t", "o") ///
	& (substr(widcode, 4, 3) != "toq") ///
	& strpos(widcode,"ptinc")==0 ///
	& strpos(widcode,"diinc")==0

// Convert monetary series to real $pastyear LCU
replace value = value/index if inlist(substr(widcode, 1, 1), "a", "m", "t", "o") & (substr(widcode, 4, 3) != "toq")

// For France, Germany & the Netherlands: old currency before 1950
replace value = value*(100*6.55957) ///
	if (year <= 1950) & (iso == "FR") & inlist(substr(widcode, 1, 1), "a", "m", "t", "o") & (substr(widcode, 4, 3) != "toq")
replace value = value*1.95583 ///
	if (year <= 1950) & (iso == "DE") & inlist(substr(widcode, 1, 1), "a", "m", "t", "o") & (substr(widcode, 4, 3) != "toq")
replace value = value*2.20371 ///
	if (year <= 1950) & (iso == "NL") & inlist(substr(widcode, 1, 1), "a", "m", "t", "o") & (substr(widcode, 4, 3) != "toq")

drop index

label data "Generated by convert-to-real.do"
save "$work_data/convert-to-real-output.dta", replace
