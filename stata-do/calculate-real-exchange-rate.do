//----------------------------------------------------------------------------//
//               Calculate real Exchange Rate .do      
//----------------------------------------------------------------------------//
* Objetive: Calculate Real exchange rate with respect to US dollar
* Note: i.e. RER<1 means real prices are lower than in the US, i.e. MER should appreciate in order to restore price parity; and converserly if RER>1

* call Data
use "$work_data/aggregate-regions-output.dta", clear

*keep relevant observations
drop if strpos(iso,"-")
keep if inlist(substr(widcode,1,6), "inyixx", "xlcusp", "xlceup", "xlcyup", "xlcusx", "xlceux", "xlcyux", "mgdpro") | widcode=="npopul999i"

drop currency
reshape wide value, i(iso year p) j(widcode) string
rename  value* *

* Calculate per-capita GDP in current MER USD for $pastyear
gen double gdp_cur_mer_usd = ((mgdpro999i/npopul999i)/inyixx999i)/xlcusx999i if year==$pastyear

* Extend the exchange rate of Germany for all the countries
gen  double xlcusx999i_DE0  = xlcusx999i if year==$pastyear & iso=="DE"
egen        xlcusx999i_DE  = mode(xlcusx999i_DE0), by(year)
drop        xlcusx999i_DE0

* Calculate per-capita GDP in current MER LCU for $pastyear
gen  double gdp_cur_mer_lcu = gdp_cur_mer_usd*xlcusx999i_DE if year==$pastyear

* Calculate per-capita GDP in current PPP EUR for $pastyear
gen  double gdp_cur_ppp_eur = ((mgdpro999i/npopul999i)/inyixx999i)/xlceup999i if year==$pastyear

* Calculate ratio PPP/MER
gen  double ratio_ppp_mer   = gdp_cur_ppp_eur/gdp_cur_mer_lcu

* Extend ratio PPP/MER of US
gen  double ratio_ppp_mer_us0 = ratio_ppp_mer if year==$pastyear & iso=="US"
egen        ratio_ppp_mer_us  = mode(ratio_ppp_mer_us0), by(year)
drop        ratio_ppp_mer_us0

* Calculate Real exchange rate to USD $pastyear
gen  double real_xrate_py0 = ratio_ppp_mer_us/ratio_ppp_mer
egen        real_xrate_py  = mode(real_xrate_py0), by(iso)
drop        real_xrate_py0

* Extend the price index of USD
gen double price_index_us0  = inyixx999i if iso=="US"
egen       price_index_us   = mode(price_index_us0),by(year)

* Extend exchange rate to USD for $pastyear
gen double xrate_to_us0  = xlcusx999i if year==$pastyear
egen       xrate_to_us   = mode(xrate_to_us0), by(iso)

* Calculate factor of calculations
gen double factor_calc= ((inyixx999i/price_index_us)/(xlcusx999i/xrate_to_us))

* Calculate Real exchange rate
gen  double value = real_xrate_py if year==$pastyear
replace     value = real_xrate_py*factor_calc if year!=$pastyear

* Clean-up and format
keep iso year p value
drop if mi(value)
gen widcode="xrerus999i"

* Generate the Metadata
preserve
	gen sixlet=substr(widcode,1,6)
	keep iso sixlet
	duplicates drop

	gen source = "See [URL][URL_TEXT]DINA guidelines[/URL_TEXT][URL_LINK]https://wid.world/document/distributional-national-accounts-guidelines-2020-concepts-and-methods-used-in-the-world-inequality-database/[/URL_LINK][/URL] for methodological explanations. The sources used are: [URL][URL_LINK]http://data.un.org/Explorer.aspx[/URL_LINK][URL_TEXT]UN SNA (1993)[/URL_TEXT][/URL]."
	gen method = "See [URL][URL_TEXT] WID National Accounts Series: Updated and Extended Coverage 1800-2023 [/URL_TEXT][URL_LINK] https://wid.world/document/wid-national-accounts-series-updated-and-extended-coverage-1800-2023-world-inequality-lab-technical-note-2025-02/[/URL_LINK][/URL]"

	append using "$work_data/aggregate-regions-metadata-output.dta"

	label data "Generated by calculate-real-exchange-rate.do"
	save "$work_data/calculate-real-exchange-rate-metadata-output.dta", replace
restore


* Keep only the core-territories if the year<1870
drop if (!inlist(iso, "AE", "AR", "AU", "BD", "BR", "CA", "CD", "CI", "CL") ///
		| !inlist(iso, "CN", "CO", "DE", "DK", "DZ", "EG", "ES", "ET", "FR") ///
		| !inlist(iso, "GB", "ID", "IN", "IR", "IT", "JP", "KE", "KR", "MA") /// 
		| !inlist(iso, "ML", "MM", "MX", "NE", "NG", "NL", "NO", "NZ", "OA") /// 
		| !inlist(iso, "OB", "OC", "OD", "OE", "OH", "OI", "OJ", "PH", "PK") /// 
		| !inlist(iso, "QE", "QM", "RU", "RW", "SA", "SD", "SE", "TH", "TR") ///
		| !inlist(iso, "TW", "US", "VN", "WO", "QL", "XB", "XF", "XL", "XN") ///
		| !inlist(iso, "XR", "XS", "ZA", "OK","OL")) ///
		& year<1970

//------- Temporary section ----------------------------------------------------

preserve
	* Import Data
	use "$work_data/NievasPiketty2025WBOP.dta", clear
	*drop if inlist(substr(iso, 1, 1), "X", "O") | inlist(iso, "QL", "QM","WO","QE")
	keep if origin =="I1g"
	generate p = "pall"

	recast double value

	drop origin concept
	* Generate Fivelets as defined in the Wid-Dictionary
	gen widcode = "xrerus999i"
	
	expand 2 if iso=="OH", gen (dup)
	replace     iso= "OK" if dup ==1
	
	expand 2 if iso=="OH", gen (dup2)
	replace     iso= "OL" if dup2==1
	drop dup*
	
	gen paper=1
	tempfile xratereal
	save   `xratereal'

restore
append using "`xratereal'"
* Verification
duplicates tag year iso widcode p, gen(dup)
drop if dup==1 & paper!=1
duplicates tag year iso widcode p, gen(dup2)
assert dup2==0
drop dup* paper
//-----------------------------------------------------------------------------	



*Add to the WID
*gen calc=1
append using "$work_data/aggregate-regions-output.dta"

* Verification
duplicates tag year iso widcode p, gen(dup)
assert dup==0
drop dup

* export
label data "Generated by calculate-real-exchange-rate.do"
save "$work_data/calculate-real-exchange-rate-output.dta", replace
