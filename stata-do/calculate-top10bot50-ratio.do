// Calculate Top10/Bottom50 ratio for all available income concepts

use "$work_data/World-and-regional-aggregates-output.dta", clear
keep if substr(widcode, 1, 1) == "a"
keep if (p == "p90p100" | p == "p0p50")

// Split widcode
generate onelet  = substr(widcode, 1, 1)
generate vartype = substr(widcode, 2, .)

greshape wide value, i(iso year widcode currency onelet vartype) j(p) string
generate value = valuep90p100/valuep0p50

drop if missing(value)
drop valuep90p100 valuep0p50
replace widcode = "r" + vartype
drop onelet vartype
generate p = "p0p100"

tempfile ratio
save "`ratio'"

// Put pop(i) to pop(j) hweal for GB before 1995
use "$work_data/World-and-regional-aggregates-output.dta", clear

keep if inlist(widcode, "shweal992i", "shweal992j") & iso == "GB"
reshape wide value, i(iso year p currency) j(widcode) string
replace valueshweal992j = valueshweal992i if year<1995 & missing(valueshweal992j)
reshape long
drop if missing(value)

tempfile shweal992j
save "`shweal992j'"

// save
use "$work_data/World-and-regional-aggregates-output.dta", clear
append using "`ratio'"
merge 1:1 iso year p widcode using "`shweal992j'", nogen

compress
label data "Generated by calculate-top10bot50-ratio.do"
save "$work_data/calculate-top10bot50-ratio.dta", replace
